; -*-mode: text; fill-column: 75; tab-width: 8; coding: iso-latin-1-dos -*-
; Script originally generated by the Inno Setup Script Wizard.
; -- $Id: maxima.iss.in,v 1.7 2004-07-01 09:21:09 billingd Exp $ --

[Setup]
AppName=Maxima
AppVerName=Maxima @VERSION@
AppPublisher=The Maxima Development Team
AppPublisherURL=http://maxima.sourceforge.net
AppSupportURL=http://maxima.sourceforge.net
AppUpdatesURL=http://maxima.sourceforge.net
AppVersion=@VERSION@
OutputBaseFilename=maxima-@VERSION@
DefaultDirName={sd}\Maxima-@VERSION@
DefaultGroupName=Maxima-@VERSION@
InfoBeforeFile=interfaces\xmaxima/win32\InfoBefore.txt
AllowNoIcons=yes
LicenseFile=@prefix@\share\maxima\@VERSION@\doc\COPYING
Compression=lzma/ultra
SolidCompression=yes
Uninstallable=yes
UninstallFilesDir={app}\uninst
; uncomment the following line if you want your installation to run on NT 3.51 too.
; MinVersion=4,3.51

[Tasks]
Name: "desktopicon"; Description: "Create a &desktop icon"; GroupDescription: "Additional icons:"; MinVersion: 4,4

[Files]
Source: "@prefix@\bin\xmaxima.exe"; DestDir: "{app}\bin\"; Flags: ignoreversion;
Source: "@prefix@\bin\maxima.bat"; DestDir: "{app}\bin\"; Flags: ignoreversion; AfterInstall: ReplaceLine({app});
Source: "@prefix@\readme.txt"; DestDir: "{app}"; Flags: isreadme;
Source: "@prefix@\*.*"; DestDir: "{app}\";  Flags: recursesubdirs

[Icons]
Name: "{group}\XMaxima @VERSION@"; Filename: "{app}\bin\xmaxima.exe"; IconFilename: "{app}\share\maxima\@VERSION@\xmaxima\maxima-icon.ico"
Name: "{group}\Introduction"; Filename: "{app}\share\maxima\@VERSION@\doc\html\intromax.html"; WorkingDir: "{app}\share\maxima\@VERSION@"
Name: "{group}\Reference Manual"; Filename: "{app}\share\maxima\@VERSION@\doc\html\maxima_toc.html"; WorkingDir: "{app}\share\maxima\@VERSION@"
Name: "{group}\README"; Filename: "{app}\readme.txt"
Name: "{userdesktop}\Maxima"; Filename: "{app}\bin\xmaxima.exe"; MinVersion: 4,4; Tasks: desktopicon; IconFilename: "{app}\share\maxima\@VERSION@\xmaxima\maxima-icon.ico"
Name: "{group}\Uninstall"; Filename:"{uninstallexe}"

[Run]
Filename: "{app}\bin\xmaxima.exe"; Description: "Launch Maxima"; Flags: postinstall skipifsilent

[Code]
{ Based on code from Inno Setup Extensions Knowledge Base
  Article 14 - How to replace a line in a text file
  http://www13.brinkster.com/vincenzog/isxart.asp?idart=14
  Author: Stefan Bracke }

{ Note: Functions called by AfterInstall can have maximum of one argument }

procedure ReplaceLine(App : String);
var
  iLineCounter : Integer;
  a_strTextfile : TArrayOfString;
  strFilename : String;
  strFind : String;
  strNewLine : String;
begin
  strFilename := App + '\bin\maxima.bat';
  strFind := 'set maxima_prefix';
  strNewLine := 'set maxima_prefix='+App;
  
  { Load textfile into string array }
  LoadStringsFromFile(strFilename, a_strTextfile);
  
  { Search through all textlines for given text }
  for iLineCounter := 0 to GetArrayLength(a_strTextfile)-1 do
    begin
      { Overwrite textline when text searched for is part of it }
      if (Pos(strFind, a_strTextfile[iLineCounter]) > 0) then
        a_strTextfile[iLineCounter] := strNewLine;
    end;

  { Save string array to textfile (overwrite, no append!) }
  SaveStringsToFile(strFilename, a_strTextfile, False);

end;
